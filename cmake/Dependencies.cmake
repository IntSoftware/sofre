function(use_or_fetch_package)
    set(options)
    set(oneValueArgs
        NAME
        VERSION
        GIT_REPOSITORY
        GIT_TAG
        ALIAS_TARGET
    )
    set(multiValueArgs
        CANDIDATE_TARGETS
    )

    cmake_parse_arguments(PKG
        "${options}"
        "${oneValueArgs}"
        "${multiValueArgs}"
        ${ARGN}
    )

    if (NOT PKG_NAME OR NOT PKG_VERSION)
        message(FATAL_ERROR
            "use_or_fetch_package requires NAME, VERSION, and ALIAS_TARGET"
        )
    endif()

    # external directory name with version number
    set(PKG_EXTERNAL_DIR
        ${CMAKE_SOURCE_DIR}/external/${PKG_NAME}-${PKG_VERSION}
    )

    set(USE_EXTERNAL_PACKAGE FALSE)

    # 1. Prefer external/<name>-<version>
    if (EXISTS ${PKG_EXTERNAL_DIR}/CMakeLists.txt)
        message(STATUS "Using external ${PKG_NAME} ${PKG_VERSION}" )
        add_subdirectory(${PKG_EXTERNAL_DIR})
        set(USE_EXTERNAL_PACKAGE TRUE)
    endif()

    # 2. Try installed package
    if (NOT USE_EXTERNAL_PACKAGE)

        find_package(${PKG_NAME} QUIET CONFIG EXACT ${PKG_VERSION})

        if (${PKG_NAME}_FOUND)
            message(STATUS
                "Found installed ${PKG_NAME} ${${PKG_NAME}_VERSION}"
            )

        else()
            message(STATUS
                "${PKG_NAME} version ${PKG_VERSION} not found"
            )
            set(USE_EXTERNAL_PACKAGE TRUE)
        endif()

    endif()

    # 3. FetchContent if not found
    if (USE_EXTERNAL_PACKAGE
        AND NOT EXISTS ${PKG_EXTERNAL_DIR}/CMakeLists.txt)

        message(STATUS
            "Fetching ${PKG_NAME} ${PKG_VERSION}"
        )

        include(FetchContent)

        FetchContent_Declare(
            ${PKG_NAME}
            GIT_REPOSITORY ${PKG_GIT_REPOSITORY}
            GIT_TAG        ${PKG_GIT_TAG}
            SOURCE_DIR     ${PKG_EXTERNAL_DIR}
        )

        FetchContent_MakeAvailable(${PKG_NAME})

    endif()

    # Set target name (alias)
    if (PKG_ALIAS_TARGET)
        if (NOT TARGET ${PKG_ALIAS_TARGET})
            foreach(candidate IN LISTS PKG_CANDIDATE_TARGETS)
                if (TARGET ${candidate})
                    add_library(${PKG_ALIAS_TARGET} ALIAS ${candidate})
                    return()
                endif()
            endforeach()

            message(FATAL_ERROR
                "Failed to resolve ${PKG_NAME} targets. "
                "Candidates: ${PKG_CANDIDATE_TARGETS}"
            )
        endif()
    endif()
endfunction()
